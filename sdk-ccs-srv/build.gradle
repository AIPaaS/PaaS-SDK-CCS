dependencies {
    compile project(":sdk-ccs-base")
    compile 'com.ai:ipaas-uac:1.0'
}

publishing {
    publications {
        mavenJar(MavenPublication) {
	       	groupId group
	        artifactId "${moduleName}"
        	version moduleVersion
            artifact  jar

			artifact (sourcesJar) { classifier "sources" }
            pom.withXml {
				//Creating additional node for dependencies
				def dependenciesNode = asNode().appendNode('dependencies')

				//Defining configuration names from which dependencies will be taken (debugCompile or releaseCompile and compile)
				def configurationNames = ['compile']

				configurationNames.each { configurationName ->
					configurations[configurationName].allDependencies.each {
						if (it.group != null && it.name != null) {
							def dependencyNode = dependenciesNode.appendNode('dependency')
							dependencyNode.appendNode('groupId', it.group)
							dependencyNode.appendNode('artifactId', it.name)
							dependencyNode.appendNode('version', it.version)

							//If there are any exclusions in dependency
							if (it.excludeRules.size() > 0) {
								def exclusionsNode = dependencyNode.appendNode('exclusions')
								it.excludeRules.each { rule ->
									def exclusionNode = exclusionsNode.appendNode('exclusion')
									exclusionNode.appendNode('groupId', rule.group)
									exclusionNode.appendNode('artifactId', rule.module)
								}
							}
						}
					}
				}
			}
        }
    }
    repositories {
        maven {
            if(moduleVersion.endsWith('-SNAPSHOT')) {
                url publishSnapShotsURL
            } else {
                url publishReleaseURL
            }
            credentials {
                username = repoUser
				password = repoPassword
            }
        }
    }
}